" MIT License. Copyright (c) 2013-2021 Bailey Ling et al.
" vim: et ts=2 sts=2 sw=2

scriptencoding utf-8

let s:parts = {}

" PUBLIC API {{{

function! airline#parts#define(key, config)
  let s:parts[a:key] = get(s:parts, a:key, {})
  if exists('g:airline#init#bootstrapping')
    call extend(s:parts[a:key], a:config, 'keep')
  else
    call extend(s:parts[a:key], a:config, 'force')
  endif
endfunction

function! airline#parts#define_function(key, name)
  call airline#parts#define(a:key, { 'function': a:name })
endfunction

function! airline#parts#define_text(key, text)
  call airline#parts#define(a:key, { 'text': a:text })
endfunction

function! airline#parts#define_raw(key, raw)
  call airline#parts#define(a:key, { 'raw': a:raw })
endfunction

function! airline#parts#define_minwidth(key, width)
  call airline#parts#define(a:key, { 'minwidth': a:width })
endfunction

function! airline#parts#define_condition(key, predicate)
  call airline#parts#define(a:key, { 'condition': a:predicate })
endfunction

function! airline#parts#define_accent(key, accent)
  call airline#parts#define(a:key, { 'accent': a:accent })
endfunction

function! airline#parts#define_empty(keys)
  for key in a:keys
    call airline#parts#define_raw(key, '')
  endfor
endfunction

function! airline#parts#get(key)
  return get(s:parts, a:key, {})
endfunction

" }}}

function! airline#parts#mode()
  let part = airline#parts#get('mode')
  let minwidth = get(part, 'minwidth', 79)
  return airline#util#shorten(get(w:, 'airline_current_mode', ''), minwidth, 1)
endfunction

function! airline#parts#crypt()
  return g:airline_detect_crypt && exists("+key") && !empty(&key) ? g:airline_symbols.crypt : ''
endfunction

function! airline#parts#paste()
  return g:airline_detect_paste && &paste ? g:airline_symbols.paste : ''
endfunction

" Sources:
" https://ftp.nluug.nl/pub/vim/runtime/spell/
" https://en.wikipedia.org/wiki/Regional_indicator_symbol
let s:flags = {
                  \ 'af_za': 'ðŸ‡¿ðŸ‡¦[af]',
                  \ 'am_et': 'ðŸ‡­ðŸ‡º[am]',
                  \ 'bg_bg': 'ðŸ‡§ðŸ‡¬',
                  \ 'br_fr': 'ðŸ‡«ðŸ‡·[br]',
                  \ 'ca_es': 'ðŸ‡ªðŸ‡¸[ca]',
                  \ 'cs_cz': 'ðŸ‡¨ðŸ‡¿',
                  \ 'cy_gb': 'ðŸ‡¬ðŸ‡§[cy]',
                  \ 'da_dk': 'ðŸ‡©ðŸ‡°',
                  \ 'de'   : 'ðŸ‡©ðŸ‡ª',
                  \ 'de_19': 'ðŸ‡©ðŸ‡ª[19]',
                  \ 'de_20': 'ðŸ‡©ðŸ‡ª[20]',
                  \ 'de_at': 'ðŸ‡©ðŸ‡ª[at]',
                  \ 'de_ch': 'ðŸ‡©ðŸ‡ª[ch]',
                  \ 'de_de': 'ðŸ‡©ðŸ‡ª',
                  \ 'el_gr': 'ðŸ‡¬ðŸ‡·',
                  \ 'en':    'ðŸ‡¬ðŸ‡§',
                  \ 'en_au': 'ðŸ‡¦ðŸ‡º',
                  \ 'en_ca': 'ðŸ‡¨ðŸ‡¦',
                  \ 'en_gb': 'ðŸ‡¬ðŸ‡§',
                  \ 'en_nz': 'ðŸ‡³ðŸ‡¿',
                  \ 'en_us': 'ðŸ‡ºðŸ‡¸',
                  \ 'es':    'ðŸ‡ªðŸ‡¸',
                  \ 'es_es': 'ðŸ‡ªðŸ‡¸',
                  \ 'es_mx': 'ðŸ‡²ðŸ‡½',
                  \ 'fo_fo': 'ðŸ‡«ðŸ‡´',
                  \ 'fr_fr': 'ðŸ‡«ðŸ‡·',
                  \ 'ga_ie': 'ðŸ‡®ðŸ‡ª',
                  \ 'gd_gb': 'ðŸ‡¬ðŸ‡§[gd]',
                  \ 'gl_es': 'ðŸ‡ªðŸ‡¸[gl]',
                  \ 'he_il': 'ðŸ‡®ðŸ‡±',
                  \ 'hr_hr': 'ðŸ‡­ðŸ‡·',
                  \ 'hu_hu': 'ðŸ‡­ðŸ‡º',
                  \ 'id_id': 'ðŸ‡®ðŸ‡©',
                  \ 'it_it': 'ðŸ‡®ðŸ‡¹',
                  \ 'ku_tr': 'ðŸ‡¹ðŸ‡·[ku]',
                  \ 'la'   : 'ðŸ‡®ðŸ‡¹[la]',
                  \ 'lt_lt': 'ðŸ‡±ðŸ‡¹',
                  \ 'lv_lv': 'ðŸ‡±ðŸ‡»',
                  \ 'mg_mg': 'ðŸ‡²ðŸ‡¬',
                  \ 'mi_nz': 'ðŸ‡³ðŸ‡¿[mi]',
                  \ 'ms_my': 'ðŸ‡²ðŸ‡¾',
                  \ 'nb_no': 'ðŸ‡³ðŸ‡´',
                  \ 'nl_nl': 'ðŸ‡³ðŸ‡±',
                  \ 'nn_no': 'ðŸ‡³ðŸ‡´[ny]',
                  \ 'ny_mw': 'ðŸ‡²ðŸ‡¼',
                  \ 'pl_pl': 'ðŸ‡µðŸ‡±',
                  \ 'pt':    'ðŸ‡µðŸ‡¹',
                  \ 'pt_br': 'ðŸ‡§ðŸ‡·',
                  \ 'pt_pt': 'ðŸ‡µðŸ‡¹',
                  \ 'ro_ro': 'ðŸ‡·ðŸ‡´',
                  \ 'ru'   : 'ðŸ‡·ðŸ‡º',
                  \ 'ru_ru': 'ðŸ‡·ðŸ‡º',
                  \ 'ru_yo': 'ðŸ‡·ðŸ‡º[yo]',
                  \ 'rw_rw': 'ðŸ‡·ðŸ‡¼',
                  \ 'sk_sk': 'ðŸ‡¸ðŸ‡°',
                  \ 'sl_si': 'ðŸ‡¸ðŸ‡®',
                  \ 'sr_rs': 'ðŸ‡·ðŸ‡¸',
                  \ 'sv_se': 'ðŸ‡¸ðŸ‡ª',
                  \ 'sw_ke': 'ðŸ‡°ðŸ‡ª',
                  \ 'tet_id': 'ðŸ‡®ðŸ‡©[tet]',
                  \ 'th'   : 'ðŸ‡¹ðŸ‡­',
                  \ 'tl_ph': 'ðŸ‡µðŸ‡­',
                  \ 'tn_za': 'ðŸ‡¿ðŸ‡¦[tn]',
                  \ 'uk_ua': 'ðŸ‡ºðŸ‡¦',
                  \ 'yi'   : 'ðŸ‡»ðŸ‡®',
                  \ 'yi_tr': 'ðŸ‡¹ðŸ‡·',
                  \ 'zu_za': 'ðŸ‡¿ðŸ‡¦[zu]',
      \ }
" Also support spelllang without region codes
let s:flags_noregion = {}
for s:key in keys(s:flags)
  let s:flags_noregion[split(s:key, '_')[0]] = s:flags[s:key]
endfor

function! airline#parts#spell()
  let spelllang = g:airline_detect_spelllang ? printf(" [%s]", toupper(substitute(&spelllang, ',', '/', 'g'))) : ''
  if g:airline_detect_spell && (&spell || (exists('g:airline_spell_check_command') && eval(g:airline_spell_check_command)))

    if g:airline_detect_spelllang !=? '0' && g:airline_detect_spelllang ==? 'flag'
      let spelllang = tolower(&spelllang)
      if has_key(s:flags, spelllang)
        return s:flags[spelllang]
      elseif has_key(s:flags_noregion, spelllang)
        return s:flags_noregion[spelllang]
      endif
    endif

    let winwidth = airline#util#winwidth()
    if winwidth >= 90
      return g:airline_symbols.spell . spelllang
    elseif winwidth >= 70
      return g:airline_symbols.spell
    elseif !empty(g:airline_symbols.spell)
      return split(g:airline_symbols.spell, '\zs')[0]
    endif
  endif
  return ''
endfunction

function! airline#parts#iminsert()
  if g:airline_detect_iminsert && &iminsert && exists('b:keymap_name')
    return toupper(b:keymap_name)
  endif
  return ''
endfunction

function! airline#parts#readonly()
  " only consider regular buffers (e.g. ones that represent actual files,
  " but not special ones like e.g. NERDTree)
  if !empty(&buftype) || airline#util#ignore_buf(bufname('%'))
    return ''
  endif
  if &readonly && !filereadable(bufname('%'))
    return '[noperm]'
  else
    return &readonly ? g:airline_symbols.readonly : ''
  endif
endfunction

function! airline#parts#filetype()
  return (airline#util#winwidth() < 90 && strlen(&filetype) > 3)
        \ ? matchstr(&filetype, '...'). (&encoding is? 'utf-8' ? "\u2026" : '>')
        \ : &filetype
endfunction

function! airline#parts#ffenc()
  let expected = get(g:, 'airline#parts#ffenc#skip_expected_string', '')
  let bomb     = &bomb ? '[BOM]' : ''
  let noeolf   = &eol ? '' : '[!EOL]'
  let ff       = strlen(&ff) ? '['.&ff.']' : ''
  if expected is# &fenc.bomb.noeolf.ff
    return ''
  else
    return &fenc.bomb.noeolf.ff
  endif
endfunction

function! airline#parts#executable()
  if exists("*getfperm") && getfperm(bufname('%')) =~ 'x'
    return g:airline_symbols.executable
  else
    return ''
  endif
endfunction
